
ZX81 LIST utility        V051292
(C) Jack Raats, Steenbergen 1992
Email:     2:285/751@fidonet.org
          27:1331/304@signet.org

FILE: alien.p

----- SYSTEM VARIABLES -----

PROG  : 16509
D_FILE: 26533
VARS  : 27326
E_LINE: 28128
STKBOT: 28128
STKEND: 28128

----- LEGENDA -----

[A] means  INVERSE A
{A} means GRAPHICS A

----- START OF LISTING -----

   1 REM 0000:>{T}EACS DACS CY+[)]DEF PROC     A PUSH ACS 9ACS 9ACS 9ACS 9ACS =FAST SGN ;INDENT - ;WHEN ;GOSUB OFF#RND;){1} ;CODE RNDTAN *??RETURN {Q}IF CHAR PUSH {1}[ ][<][9]C{5}4{A}   {T} :{1}TAN {T} : TAN 00000000000000000000000000000
   2 LET HIS=0
   3 REM ALIEN:                     COPYRIGHT P.MERRIMAN(6/5/82)
   4 GOSUB 9000
   5 GOSUB 8000
   9 POKE 16418,0
  10 PRINT 
  11 PRINT 
  12 PRINT 
  14 PRINT "    {E}{E}{7}{5}"
  15 PRINT "   {E}{E}A {5}"
  20 PRINT "{E}{7}{7}{R}{6} {6}{W}"
  25 PRINT "{5}   {5}  {2}{E}{W}{6}"
  30 PRINT "{5} B   C {5} {E}{7}{W}{6}"
  35 PRINT "{5}   {5}   {5}D{5} {8}{2}{7}{W}{6}"
  40 PRINT "{E}{7} {7}{7}{R}     E  F{5}{2}{7}{W}     {E}{4}"
  45 PRINT "{5}       {5} {5} {8}  {5} G{2}{W}  {E}{Q}{7}{7}{W}{6}"
  50 PRINT "{5}    {8}{7}{7}{W} {W} {Q}{6} {W}{6} {6}{Q}{W}{Q}[ ]{6}{6} {6}{Q}{7}{R}{4}"
  55 PRINT "[ ][ ] H                {H}         [ ]"
  60 PRINT "{5}    {8}{6}{6}{E} {E} {R}{7} {E}{7} {7}{R}{E}{R}[ ]{7}{7} {7}{R}{6}{Q}{1}"
  65 PRINT "{5}       {5} {5} {8}  {5} I{E}{E}  {2}{R}{6}{6}{E}{7}"
  70 PRINT "{W}{6} {6}{6}{Q}     K  J{5}{E}{6}{E}     {2}{1}"
  75 PRINT "{5}   {5}   {5}L{5} {8}{E}{6}{E}{7}"
  80 PRINT "{5} N   M {5} {W}{6}{E}{7}"
  85 PRINT "{5}   {5}  {E}{W}{E}{7}"
  90 PRINT "{W}{6}{6}{Q}{7} {7}{E}"
  95 PRINT "   {2}{W}O {5}"
 100 PRINT "    {2}{W}{6}{5}"
 105 PRINT AT 20,10;"YOU ARE CARRYING:-"
 110 PRINT AT 1,0;"NO. OF CREW=";N
 112 PRINT AT 0,15;"ALIEN PTS=";HP
 113 PRINT AT 0,0;"SCORE=";SCR
 114 SLOW 
 115 LET FLG=0
 150 LET SC=(PEEK 16396+256*PEEK 16397)+1
 155 LET XM=5
 160 LET YM=14
 165 LET CN=0
 170 LET FL=0
 172 LET X=42
 173 LET Y=18
 180 GOSUB 208
 185 GOSUB 2000
 190 GOTO 180
 209 LET A$=INKEY$
 210 IF X=XM AND Y=YM THEN GOTO 3000
 212 IF A$="L" THEN GOSUB 550
 215 IF A$="F" THEN GOSUB 1000
 220 LET X2=X+(A$="E" OR A$="D" OR A$="C")-(A$="Q" OR A$="A" OR A$="Z")
 225 LET Y2=Y+(A$="Q" OR A$="W" OR A$="E")-(A$="Z" OR A$="X" OR A$="C")
 230 UNPLOT X,Y
 235 POKE 16519,Y2
 240 POKE 16521,X2
 245 LET A=USR 16518
 250 IF A>37 AND A<62 THEN GOTO 350
 255 PLOT X2,Y2
 260 LET B=USR 16518
 265 IF A=B THEN GOTO 276
 270 LET X=X2
 273 LET Y=Y2
 274 IF X>36 AND X<44 AND Y>17 AND Y<20 THEN POKE SC+416,136
 280 RETURN 
 290 PLOT X,Y
 300 RETURN 
 350 LET D=A-37
 360 IF CA=0 THEN GOTO 368
 362 GOTO 290
 368 LET CA=D
 369 LET C$=M$(B(CA))
 370 PRINT AT 22,10;C$
 375 GOTO 252
 550 REM LEAVE
 551 IF CA=0 THEN RETURN 
 552 GOSUB 7500
 555 POKE 16521,X+(2*XD)
 560 POKE 16519,Y+(2*YD)
 565 LET A=USR 16518
 570 LET PS=PEEK 16580+256*PEEK 16581
 575 IF A<>0 THEN RETURN 
 580 POKE PS,CA+37
 585 LET CA=0
 590 LET C$=""
 592 PRINT AT 22,10;"                    "
 595 RETURN 
1000 REM FIGHT
1005 GOSUB 7500
1010 POKE 16521,X+(2*XD)
1015 POKE 16519,Y+(2*YD)
1020 LET A=USR 16518
1025 IF A<>1 AND A<>2 AND A<>4 AND A<>135 THEN RETURN 
1030 POKE 16521,XM
1035 POKE 16519,YM
1040 LET A1=USR 16518
1045 IF A<>A1 THEN RETURN 
1050 LET PS=PEEK 16580+256*PEEK 16581
1055 IF CA=0 THEN RETURN 
1060 POKE PS,CA+37
1070 LET C$=""
1075 PRINT AT 22,10;"                    "
1080 LET FLG=1
1085 LET D=F(B(CA))
1090 PRINT AT 1,18;E$(D)
1095 IF D>4 THEN GOTO 1110
1100 FOR D=1 TO 50
1101 NEXT D
1105 GOTO 1950
1110 LET D4=INT (RND*4)+2
1112 LET SCR=SCR+5
1115 FOR I=1 TO D4
1120 GOSUB 208
1125 NEXT I
1130 IF D<6 THEN GOTO 1950
1135 LET D=D-5
1140 LET HP=HP-(10*D)-INT (RND*11)
1142 LET SCR=SCR+(10*D)+INT (RND*11)
1143 IF HP<=0 THEN GOTO 5000
1144 PRINT AT 0,25;"      "
1145 PRINT AT 0,25;HP
1950 PRINT AT 1,18;"              "
1955 PRINT AT 0,6;SCR
1990 LET CA=0
1999 RETURN 
2001 IF FLG<>1 THEN UNPLOT XM,YM
2002 IF CN>0 THEN GOTO 2100
2003 IF CN<=0 AND RND>.9 AND ABS (X-XM)>5 AND ABS (Y-YM)>5 THEN GOSUB 2150
2004 IF CN>0 THEN GOTO 2080
2005 IF X=XM AND Y=YM THEN GOTO 3000
2011 LET YD=Y-YM
2013 LET XD=X-XM
2015 LET Y3=YM+SGN YD
2020 LET X3=XM+SGN XD
2021 UNPLOT X,Y
2025 POKE 16521,X3
2030 POKE 16519,Y3
2035 LET A=USR 16518
2037 IF A>37 AND A<52 OR A=189 OR A=136 THEN GOTO 2080
2038 PLOT X,Y
2039 LET FLG=0
2040 LET Y4=Y3/2<>INT (Y3/2)
2045 LET X4=X3/2<>INT (X3/2)
2050 PRINT AT 21,31;CHR$ A
2055 PLOT 62+X4,Y4
2060 IF (PEEK (SC+724))=A THEN GOTO 2080
2072 LET YM=Y3
2073 LET XM=X3
2077 IF ABS (Y-YM)<10 AND ABS (X-XM)<10 THEN PLOT XM,YM
2079 RETURN 
2084 LET YD=INT (RND*3)-1
2085 LET XD=INT (RND*3)-1
2087 IF RND>.6 THEN RETURN 
2090 GOTO 2015
2100 LET CN=CN-1
2105 RETURN 
2150 LET CN=INT (RND*10)+1
2160 RETURN 
3000 PRINT AT 2,14;"A SICKENING SCREAM"
3001 PRINT TAB 14;"RINGS THROUGH THE"
3002 PRINT TAB 14;"SHIP.IT GOT YOU"
3003 FOR D=1 TO 75
3004 NEXT D
3008 LET N=N-1
3009 IF N=0 THEN GOTO 4000
3010 LET C$=""
3015 LET CA=0
3020 CLS 
3035 GOTO 10
4000 REM END
4005 CLS 
4007 PRINT "       THE FATAL DEFEAT"
4008 PRINT "       {7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}"
4010 PRINT ,,"1000 YEARS AFTER THIS GRUESOME"
4011 PRINT "EPISODE A STRANGE SHIP APPEARS  IN THE SOLAR SYSTEM. AS IT      SWINGS TOWARDS THEM THE PEOPLE  OF EARTH PUZZLE OVER ITS ORIGINS,WHILE ON BOARD THE ANCIENT     NOSTRODAMUS THE ALIEN BROOD SITSAND WAITS."
4014 PRINT ,,,,,,"YOUR ATTEMPT TO DEFEAT THE      CREATURE, THOUGH NOBLE, FAILED  AND YOU ATTAINED A SCORE OF     ";SCR;" POINTS"
4015 IF SCR>HIS THEN LET HIS=SCR
4017 PRINT ,,"HIGH SCORE=";HIS
4020 PRINT ,,"DO YOU WANT ANOTHER GAME?"
4024 LET R$=INKEY$
4025 IF R$="" THEN GOTO 4024
4027 CLS 
4030 IF R$="Y" THEN GOTO 4
4500 STOP 
5000 CLS 
5002 LET SCR=SCR+(50*N)
5005 PRINT "       {E}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{R}"
5010 PRINT "       {5}[ ][Y][O][U][ ][A][R][E][ ][S][A][V][E][D][ ]{8}"
5015 PRINT "       {W}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{Q}"
5020 PRINT ,,,,,,"YOU MANAGED TO DESTROY THE ALIENBEFORE IT WIPED OUT YOUR CREW"
5021 PRINT ,,"YOU SCORED ";SCR;" POINTS"
5022 IF SCR>HIS THEN LET HIS=SCR
5024 PRINT ,,"HIGH SCORE=";HIS
5030 GOTO 4020
5500 STOP 
7500 REM 
7505 LET A$=INKEY$
7510 LET XD=(A$="E" OR A$="D" OR A$="C")-(A$="Q" OR A$="A" OR A$="Z")
7515 LET YD=(A$="Q" OR A$="W" OR A$="E")-(A$="Z" OR A$="X" OR A$="C")
7517 IF XD=0 AND YD=0 THEN GOTO 7505
7519 LET A$=""
7520 RETURN 
7999 STOP 
8000 FAST 
8001 LET CA=0
8002 DIM M$(15,20)
8003 LET N=4
8004 DIM Q(2)
8005 LET M$(1)="A LASER GUN"
8010 LET M$(2)="A FLAME THROWER"
8015 LET M$(3)="A WELDING TORCH"
8020 LET M$(4)="BOTTLES OF ACID"
8025 LET M$(5)="GAS GRENADES"
8030 LET M$(6)="EXPLOSIVES"
8035 LET M$(7)="A SONIC GUN"
8040 LET M$(8)="A FORCE GENERATOR"
8045 LET M$(9)="GAS GRENADES"
8050 LET M$(10)="A LASER GUN"
8055 LET M$(11)="A SONIC GUN"
8060 LET M$(12)="GAS GRENADES"
8065 LET M$(13)="EXPLOSIVES"
8070 LET M$(14)="A WELDING TORCH"
8075 LET M$(15)="A FORCE GENERATOR"
8080 DIM B(15)
8085 FOR D=1 TO 15
8090 LET D2=INT (RND*15)+1
8095 IF B(D2)<>0 THEN GOTO 8090
8100 LET B(D2)=D
8105 NEXT D
8150 REM EFFECTS
8155 DIM E$(8,15)
8160 LET E$(1)="NO EFFECT"
8165 LET E$(2)="NO EFFECT"
8170 LET E$(3)="NO EFFECT"
8175 LET E$(6)="SLIGHT DAMAGE"
8180 LET E$(7)="MINOR DAMAGE"
8185 LET E$(4)="NO EFFECT"
8190 LET E$(8)="MAJOR DAMAGE"
8195 LET E$(5)="STUNNED"
8200 DIM F(15)
8205 FOR D=1 TO 8
8210 LET D2=INT (RND*8)+1
8215 IF F(D2)<>0 THEN GOTO 8210
8220 LET F(D2)=D
8225 NEXT D
8230 LET F(9)=F(5)
8235 LET F(12)=F(5)
8240 LET F(10)=F(1)
8245 LET F(11)=F(7)
8250 LET F(13)=F(6)
8255 LET F(14)=F(3)
8260 LET F(15)=F(8)
8300 LET HP=200
8305 LET SCR=0
8310 CLS 
8500 RETURN 
9000 CLS 
9001 PRINT "DO YOU WANT INSTRUCTIONS?"
9002 IF INKEY$="Y" THEN GOTO 9005
9003 IF INKEY$="N" THEN RETURN 
9004 GOTO 9002
9005 CLS 
9006 POKE 16418,0
9007 PRINT TAB 8;"{6}{6} {4}  {4} {6}{4} {4}  {4}"
9008 PRINT TAB 8;"{5}{8} {5}  {5} {5}  {E}{4} {5}"
9009 PRINT TAB 8;"{E}{R} {5}  {5} {E}  {5}{2}{4}{5}"
9010 PRINT TAB 8;"{5}{8} {W}{6} {5} {W}{4} {5} {2}{5}"
9011 PRINT ,,"         BY P.MERRIMAN"
9012 PRINT ,,"YOU ARE ON BOARD THE TRANSPORT  SHIP ""NOSTRODAMUS"" EARTHWARD    BOUND,WHEN YOU PICK UP A MAYDAY SIGNAL FROM A NEARBY PLANET."
9015 PRINT "  WHEN YOU INVESTIGATE YOU      DISCOVER AN ANCIENT ALIEN SHIP  AND WHILE EXPLORING ONE OF THE  CREW MEMBERS IS ATTACKED."
9020 PRINT "  YOU MANAGE TO GET HIM BACK TO THE SHIP WITH THE STRANGE LIVINGCREATURE ATTACHED TO HIS FACE."
9025 PRINT "  EVENTUALLY HE DIES AND THE    ALIEN ORGANISM WHICH HAS BEEN   GROWING IN HIS STOMACH ESCAPES  AND GROWS AT AN INCREDIBLE RATE."
9026 PRINT ,,"         [P][R][E][S][S][ ][A][N][Y][ ][K][E][Y]"
9027 IF INKEY$="" THEN GOTO 9027
9028 CLS 
9030 PRINT "  EARTH FORBIDS YOU TO RETURN   WITH THE CREATURE STILL ALIVE SOYOU MUST TRY AND KILL IT."
9035 PRINT ,,,,"THE GAME SIMULATES YOUR ATTEMPTSTO DO SO AS YOU STALK ROUND THE CRAFT HOPING THE WEAPON YOU HAVEPICKED UP WILL WORK AGAINST THE CREATURE."
9036 PRINT ,,"         [P][R][E][S][S][ ][A][N][Y][ ][K][E][Y]"
9037 IF INKEY$="" THEN GOTO 9037
9038 CLS 
9040 PRINT "         HOW TO PLAY"
9045 PRINT "         {7}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}"
9050 PRINT ,,"MOVEMENT IS IN EIGHT DIRECTIONS USING THE KEYS SHOWN BELOW"
9055 PRINT ,,"Q  W  E     {E}  {T}{4}  {R}"
9057 PRINT "             {Y} {8}  {T}"
9058 PRINT "              {Y}{8} {T}"
9060 PRINT ,,"A  S  D  =  {T}{6}{4}  {6}{6}{Y}" 
9065 PRINT "            {2}      {1}"
9067 PRINT "              {T}{8} {Y}"
9068 PRINT "             {T} {8}  {Y}"
9070 PRINT "Z  X  C     {W}  {Y}{1}  {Q}"
9075 PRINT ,,"WHERE ""S"" CORRESPONDS TO YOUR   POSITION."
9080 PRINT ,,"        [P][R][E][S][S][ ][A][N][Y][ ][K][E][Y]"
9085 IF INKEY$="" THEN GOTO 9085
9090 CLS 
9095 PRINT "YOU ARE SHOWN AS A SMALL        FLASHING SQUARE.THE VARIOUS     LETTERS SCATTERED AROUND THE    SHIP ARE WEAPONS,YOU DON""T KNOW WHICH PARTICULAR ONES UNTIL YOU PICK THEM UP."
9100 PRINT ,,"  TO DO SO YOU SIMPLY MOVE      THROUGH THE LETTER AND YOU WILL PICK IT UP AUTOMATICALLY.YOU CANHOWEVER ONLY CARRY ONE OBJECT ATA TIME."
9105 PRINT ,,"THE DISPLAY WILL INDICATE THE   NAME OF THE WEAPON ,THOUGH YOU  WON""T KNOW ITS EFFECT ON THE    ALIEN UNTIL YOU TRY IT OUT"
9106 PRINT ,,"          [P][R][E][S][S][ ][A][N][Y][ ][K][E][Y]"
9107 IF INKEY$="" THEN GOTO 9107
9109 CLS 
9110 PRINT ,,"TO LEAVE THE WEAPON PRESS ""L"",  YOU WILL STOP FLASHING AND MUST NOW PRESS ONE OF THE DIRECTION  KEYS (AS USED FOR MOVEMENT) TO  INDICATE WHICH WAY IT WILL GO.  IF THERE IS NO ROOM FOR IT IN   THAT DIRECTION IT WILL NOT BE   DROPPED." 
9115 PRINT ,,"  IN ORDER TO USE THE WEAPON    AGAINST THE ALIEN HE MUST BE    WITHIN TWO MOVES OF YOU AND     THERE MUST BE ROOM TO DROP THE  WEAPON (WHICH YOU ALWAYS DO     AUTOMATICALLY AFTER FIRING AT   THE CREATURE)."   
9120 PRINT ,,"          [P][R][E][S][S][ ][A][N][Y][ ][K][E][Y]"
9122 IF INKEY$="" THEN GOTO 9122
9125 CLS 
9130 PRINT "  ONCE THESE CONDITIONS HAVE    BEEN SATISFIED YOU SIMPLY FIRE  BY PRESSING THE ""F"" KEY, WAITINGUNTIL YOU STOP FLASHING AND THENPRESSING A DIRECTION KEY TO     INDICATE WHICH DIRECTION YOU AREFIRING IN."
9135 PRINT ,,"  THE ALIEN IS ALSO DISPLAYED   AS A SINGLE PIXEL BUT CAN ONLY  BE SEEN WHEN WITHIN ABOUT TEN   MOVES FROM YOU.IN GENERAL IT    WILL MOVE DIRECTLY TOWARDS YOU  NAVIGATING ITS WAY AROUND WALLS ETC."
9140 PRINT ,,"          [P][R][E][S][S][ ][A][N][Y][ ][K][E][Y]"
9142 IF INKEY$="" THEN GOTO 9142
9143 CLS 
9145 PRINT "  THE CREATURE STARTS  WITH     200 POINTS BUT ANY DAMAGE WILL  DECREASE THIS VALUE.WHEN THE PTSGO BELOW ZERO THE CREATURE IS   DEAD."
9150 PRINT ,,"  WHEN ALL FOUR MEMBERS OF YOUR CREW HAVE BEEN KILLED THE GAME  ENDS.THE SQUARE ,""{H}"", IS AN     ENERGY BARRIER THROUGH WHICH    ONLY YOU CAN PASS.",,,"    GOOD LUCK"
9155 PRINT ,,,,"[T][O][ ][S][T][A][R][T][ ][T][H][E][ ][G][A][M][E][ ][P][R][E][S][S][ ][K][E][Y][ ]["][S]["][.][T][O][ ][G][E][T][ ][T][H][E][ ][I][N][S][T][R][U][C][T][I][O][N][S][ ][A][G][A][I][N][ ][ ][ ][P][R][E][S][S][ ][O][E][Y][ ]["][R]["][.][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]"
9160 IF INKEY$="S" THEN GOTO 9790
9165 IF INKEY$="R" THEN GOTO 9005
9170 GOTO 9160
9790 CLS 
9800 RETURN 
9997 SAVE "ALIE[N]"
9998 GOTO 1
9999 REM VERSION 2

----- END OF LISTING -----
